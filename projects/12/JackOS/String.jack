// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array chars;
    field int size;
    field int length;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (~(maxLength < 1)) {
            let chars = Array.new(maxLength);
        }
        let size = maxLength;

        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        if (~(size < 1)) {
            do Memory.deAlloc(chars);
        }
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return chars[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let chars[j] = c;

        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (length < size) {
            let chars[length] = c;
            let length = length + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (length > 0) {
            let length = length - 1;
            let chars[length] = 0;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int value, i;
        var int negateFactor;

        let negateFactor = 1;

        if (chars[0] = 45) {
            let i = 1;
            let negateFactor = -1;
        }

        while (i < length) {
            let value = (value * 10) + (chars[i] - 48);
            let i = i + 1;
        }
        return value * negateFactor;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        let length = 0;

        if (val < 0) {
            do appendChar(45);
            let val = -val;
        }

        do uintToString(val);
        
        return;
    }

    /** Sets this string to hold a representation of a non-negative integer. */
    method void uintToString(int val) {
        var int q, lastDigitValue;

        let q = val / 10;
        let lastDigitValue = val - (q * 10) + 48;

        if (~(val < 10)) {
            do uintToString(q);
            do appendChar(lastDigitValue);
        }
        else {
            do appendChar(lastDigitValue);
        }
        return;
    }

    /** Returns if the given character is a digit. */
    function bool isDigit(char c) {
        return ~(c < 0) & ~(c > 57);
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
